//`timescale 1ns/1ns

module SYNC_RAM_DP_WBE #(
  parameter DWIDTH = 32,             // Data width
  parameter AWIDTH = 14,             // Address width
  parameter DEPTH  = (1 << AWIDTH), // Memory depth
  parameter MIF_HEX = "",
  parameter MIF_BIN = ""
)(
  input clk,
  input [DWIDTH-1:0]   d0,    // Data input
  input [AWIDTH-1:0]   addr0, // Address input
  input [DWIDTH/8-1:0] wbe0,  // write-byte-enable
  input                wen0,
  output [DWIDTH-1:0]  q0,

  input [DWIDTH-1:0]   d1,    // Data input
  input [AWIDTH-1:0]   addr1, // Address input
  input [DWIDTH/8-1:0] wbe1,  // write-byte-enable
  input                wen1,
  output [DWIDTH-1:0]  q1
  );

(* ram_style = "block" *) reg [DWIDTH-1:0] mem [0:DEPTH-1];

reg [DWIDTH-1:0] read_data0_reg;
reg [DWIDTH-1:0] read_data1_reg;

integer i;
initial begin
  if (MIF_HEX != "") begin
      $readmemh(MIF_HEX, mem);
  end
  else if (MIF_BIN != "") begin
      $readmemb(MIF_BIN, mem);
  end
  else begin
    for (i = 0; i < DEPTH; i = i + 1) begin
      mem[i] = 0;
    end

    read_data0_reg = {DWIDTH{1'b0}};
    read_data1_reg = {DWIDTH{1'b0}};
  end
end


always @(posedge clk) begin
  read_data0_reg <= mem[addr0];
  if (wen0) begin
    for (i = 0; i < 4; i = i+1) begin
      if (wbe0[i])
        mem[addr0][i*8 +: 8] <= d0[i*8 +: 8];
    end
  end
end

always @(posedge clk) begin
  read_data1_reg <= mem[addr1];
  if (wen1) begin
    for (i = 0; i < 4; i = i+1) begin
      if (wbe1[i])
        mem[addr1][i*8 +: 8] <= d1[i*8 +: 8];
      end
  end
end

assign q0 = read_data0_reg;
assign q1 = read_data1_reg;

endmodule
